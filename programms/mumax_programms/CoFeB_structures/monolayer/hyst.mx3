// Эта программа моделирует MgO/CofeB/MgO

// Глобальные конфиги
OutputFormat = OVF2_TEXT
EnableDemag=false
MinimizerStop = 1e-6
Multiplier := pow(10, -4) // для перевода Oe -> T

Xcellsize := 5e-9 // в м
Ycellsize := 5e-9
Zcellsize := 0.3e-9

Xcount := 300 // по сути количество ячеек
Ycount := 300
Zcount := 3

Xsize := Xcount * Xcellsize // реальный размер
Ysize := Ycount * Ycellsize
Zsize := Zcount * Zcellsize

SetPBC(2,2,0) //периодические граничные условия
SetGridsize(Xcount, Ycount, Zcount)
SetCellsize(Xcellsize, Ycellsize, Zcellsize)

geometryShape := Cuboid(Xsize, Ysize, Zsize) // задаем кубоид
SetGeom(geometryShape)
SaveAs(geom, "cuboid") // сохраняет факт принадлежности каждой ячейки к определенному региону

DefRegion(1, geometryShape)
SaveAs(regions, "OneRegion")

// Параметры материала
Ms := 952e3 // A/m
Kanis := 4e3 // J/m^3
//Kanis := 7.5e6
Jex := 1e-11 // J/m

Msat.SetRegion(1, Ms)
AnisU.SetRegion(1, vector(0, 0, 1))
Ku1.SetRegion(1, Kanis)
Aex.SetRegion(1, Jex)

m.SetRegion(1, uniform(-1, 1, -100)) // начальная намагниченность по OZ
//m.SetRegion(1, RandomMag()) // рандомная намагниченность
SnapshotAs(m, "RandomInitMagn.png")
SaveAs(m, "RandomInitMagn")
//relax() // приходим к чему-то стабильному
//SnapshotAs(m, "BeforeHystMagn.png")
//SaveAs(m, "BeforeHystMagn")


// Гистерезис, Oe
Bmax  := 200 * Multiplier
Bstep :=  1 * Multiplier

TableAdd(B_ext)
//TableAdd(E_total)

for B:=0.0; B<=Bmax; B+=Bstep{
    B_ext = vector(0.1*B, 0, B)
    minimize()
    tablesave()
}
Save(m)

for B:=Bmax; B>=-Bmax; B-=Bstep{
    B_ext = vector(0.1*B, 0, B)
    minimize()
    tablesave()
}
Save(m)

for B:=-Bmax; B<=Bmax; B+=Bstep{
    B_ext = vector(0.1*B, 0, B)
    minimize()
    tablesave()
}

Save(m) 
SnapshotAs(m, "AfterHyst.png")
