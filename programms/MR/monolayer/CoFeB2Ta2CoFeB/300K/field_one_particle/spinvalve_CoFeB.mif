# MIF 2.1
set pi [expr {4*atan(1.0)}]
set mu0 [expr {4*$pi*1e-7}]

######### Magnetic parameters #######
set Mstop	952e3		;# A/m
set Jex		1e-11		;# J/m
set Ktop	3.125e3		;# J/m^3
############### ATLAS & MESH #################
set Xcellsize	5	;# дискретизация вдоль X [нм]
set Ycellsize	5	;# дискретизация вдоль Y [нм]
set Zcellsize	0.3	;# дискретизация вдоль Z [нм]

set Xcellsize [expr {$Xcellsize*1e-9}]
set Ycellsize [expr {$Ycellsize*1e-9}]
set Zcellsize [expr {$Zcellsize*1e-9}]

set Xcount      200	;# число ячеек вдоль оси OX
set Ycount      200	;# число ячеек вдоль оси OY

set Xsize [expr {$Xcellsize * $Xcount}]
set Ysize [expr {$Ycellsize * $Ycount}]

set htop        0.9e-9  ;# толщина верхнего слоя, нм
set hadd        2.4e-9  ;# добавочное поднятие частицы над платформой, нм

################ Magnetic particle ###################
set moment      287e-18     ;# Magnetic moment of particle, Anm^2
set rpart       70e-9      ;# R particle, nm

set xpart [ expr {$Xsize / 2} ]
set ypart [ expr {$Ysize / 2} ]
set zpart [ expr { $htop + $rpart + $hadd} ]

Specify Oxs_MultiAtlas:atlas [subst {
    atlas { Oxs_BoxAtlas {
        name top
        xrange {0 $Xsize}
        yrange {0 $Ysize}
        zrange {0 $htop}
    } }
} ]

Specify Oxs_RectangularMesh:mesh [subst {
  cellsize {$Xcellsize $Ycellsize $Zcellsize}
  atlas :atlas
}]

proc FieldPart { x y z } {
  global rpart moment xpart ypart zpart pi
  set rx [ expr {$x - $xpart} ]
  set ry [ expr {$y - $ypart} ]
  set rz [ expr {$z - $zpart} ]
  set currmoment [ expr {($moment / (70e-9)**3) * ($rpart**3)} ]
  set r  [ expr { ($rx)**2 + ($ry)**2 + ($rz)**2 } ]
  set r  [ expr {$r**0.5} ]
  set Hz [ expr { (1/(4*$pi))*(3*(-1*$currmoment)*($rz**2)/($r**5) + $currmoment/($r**3)) } ]
  set Hy [ expr { (1/(4*$pi))*(3*(-1*$currmoment)*$rz*$ry)/($r**5) } ]
  set Hx [ expr { (1/(4*$pi))*(3*(-1*$currmoment)*$rz*$rx)/($r**5) } ]
  #set Hx [ expr { 0.00001*$Hx } ]
  #set Hy [ expr { 0.00001*$Hy } ]
  return [list $Hx $Hy $Hz]
}

Specify Oxs_FixedZeeman:fieldofpart [subst {
  field { Oxs_ScriptVectorField::fieldp {
     atlas :atlas
     script FieldPart
     script_args rawpt
     }
  }
}]

Specify Oxs_Exchange6Ngbr [ subst {
  atlas :atlas
  default_A 0
  A {
    top top $Jex
  }
} ]

Specify Oxs_UniaxialAnisotropy [ subst {
   K1  { Oxs_AtlasScalarField {
      atlas :atlas
      default_value 0
      values {
         top    $Ktop
      }
   }}
   axis { Oxs_AtlasVectorField {
      atlas :atlas
      default_value {0 0 1}
      values {
         top    {0 0 1}
      }
   }}
} ]

Specify Oxs_CGEvolve {}

Specify Oxs_MinDriver [ subst {
 basename CoFeB
 evolver Oxs_CGEvolve
 stopping_mxHxm 0.1
 mesh :mesh
 Ms  { Oxs_AtlasScalarField {
      atlas :atlas
      default_value 0
      values {
         top $Mstop
      }
 }}
 m0  { Oxs_AtlasVectorField {
     atlas :atlas
     norm 1.0
     default_value {0 0 1}
     values {
        top     {0 0 1}
     }
 }}
 normalize_aveM_output 0
 report_max_spin_angle 1
} ]
