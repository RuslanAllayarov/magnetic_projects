# MIF 2.1
set pi [expr {4*atan(1.0)}]
set mu0 [expr {4*$pi*1e-7}]

######### Magnetic parameters #######
set Mstop	952e3		;# A/m
set Jex		1e-11		;# J/m
set Ktop	3.125e3		;# J/m^3
############### ATLAS & MESH #################
set Xcellsize	5	;# дискретизация вдоль X [нм]
set Ycellsize	5	;# дискретизация вдоль Y [нм]
set Zcellsize	0.3	;# дискретизация вдоль Z [нм]

set Xcellsize [expr {$Xcellsize*1e-9}]
set Ycellsize [expr {$Ycellsize*1e-9}]
set Zcellsize [expr {$Zcellsize*1e-9}]

set Xcount      300	;# число ячеек вдоль оси OX
set Ycount      300	;# число ячеек вдоль оси OY

set Xsize [expr {$Xcellsize * $Xcount}]
set Ysize [expr {$Ycellsize * $Ycount}]

set htop        0.9e-9  ;# толщина верхнего слоя, нм
set hadd        2.4e-9  ;# добавочное поднятие частицы над платформой, нм

################ Magnetic particle ###################
#set moment      287e-18     ;# Magnetic moment of particle, Am^2
set moment	0.0
#set rpart       140e-9      ;# R particle, m
set rpart       280e-9      ;# R particle, m



set xpart [ expr {$Xsize / 2} ]
set ypart [ expr {$Ysize / 2} ]
set zpart [ expr { $htop + $rpart + $hadd} ]

################ External field ######################
################ Это меняем и только #################
set angle       0       ;# angle = (Hext, normal)
set Hext        20     ;# module of external field

################ Add field dependence ###############
################ NPs moment #########################
#set coef        0.087208e-19    ;# переводит поле из A/m в добавку к моменту в Am^2, в m^3
set coef        0.0


Specify Oxs_MultiAtlas:atlas [subst {
    atlas { Oxs_BoxAtlas {
        name top
        xrange {0 $Xsize}
        yrange {0 $Ysize}
        zrange {0 $htop}
    } }
} ]


############## MESH #############################
#Specify Oxs_RectangularMesh:mesh [subst {
#  cellsize {$Xcellsize $Ycellsize $Zcellsize}
#  atlas :atlas
#}]

Specify Oxs_PeriodicRectangularMesh:mesh [subst {
  cellsize {$Xcellsize $Ycellsize $Zcellsize}
  atlas :atlas
  periodic xy
}]

############### ZEEMAN ##########################

# С учетом вращения наночастицы за полем
# и увеличения момента частицы при увеличении поля
# работает только на состоянии AP+
proc FieldPartRotateAcceler { x y z } {
  global rpart moment xpart ypart zpart pi angle Hext coef
  # переводим градусы в радианы
  set anglerad [ expr { ($angle / 180.0) * $pi} ]
  set rx [ expr {$x - $xpart} ]
  set ry [ expr {$y - $ypart} ]
  set rz [ expr {$z - $zpart} ]
  # нужно добавить линейную добавку к моменту по полю
  # multpiplier - Oe -> A/m
  set multiplier [ expr {1000 / (4 * $pi)} ]
  set linadd [ expr { $coef*$Hext*$multiplier } ]
  set newmoment [ expr {$moment + $linadd} ]
  set currmoment [ expr {($newmoment / (140e-9)**3) * ($rpart**3)} ]

  set r  [ expr { ($rx)**2 + ($ry)**2 + ($rz)**2 } ]
  set r  [ expr {$r**0.5} ]

  set Hz [ expr { (1/(4*$pi))*(3*$rz*$currmoment*(sin($anglerad)*$ry + cos($anglerad)*$rz)/($r**5) - $currmoment*cos($anglerad)/($r**3)) } ]
  set Hy [ expr { (1/(4*$pi))*(3*$ry*$currmoment*(sin($anglerad)*$ry + cos($anglerad)*$rz)/($r**5) - $currmoment*sin($anglerad)/($r**3)) } ]
  set Hx [ expr { (1/(4*$pi))*(3*$rx*$currmoment*(sin($anglerad)*$ry + cos($anglerad)*$rz)/($r**5)) } ]

  # Add external field
  # (Hextx,Hexty,Hextz)
  # будем считать, что поле внешнее лежит в плоскости OYZ
  set hextx [ expr { $multiplier * 0 }  ]
  set hexty [ expr { $multiplier * $Hext * sin($anglerad) }  ]
  set hextz [ expr { $multiplier * $Hext * cos($anglerad) }  ]
  set hx [ expr { $Hx + $hextx } ]
  set hy [ expr { $Hy + $hexty } ]
  set hz [ expr { $Hz + $hextz } ]
  return [list $hx $hy $hz]
}

############ Старое поле #####################
Specify Oxs_FixedZeeman:fieldofpart [subst {
  field { Oxs_ScriptVectorField::fieldp {
     atlas :atlas
     script FieldPartRotateAcceler
     script_args rawpt
     }
  }
}]
##############
# Добавляем считывание поля
#Specify Oxs_FileVectorField:fieldofparts {
#   file data/90deg_with_N_particles/field_100_Oe_30_NPs.csv
#   spatial_scaling {1 1 1}
#   spatial_offset  {0 0 0}
#}
#
#Specify Oxs_FixedZeeman:FileField {
#  field :fieldofparts
#}
###############


################### OTHER #######################
Specify Oxs_Exchange6Ngbr [ subst {
  atlas :atlas
  default_A 0
  A {
    top top $Jex
  }
} ]

Specify Oxs_UniaxialAnisotropy [ subst {
   K1  { Oxs_AtlasScalarField {
      atlas :atlas
      default_value 0
      values {
         top    $Ktop
      }
   }}
   axis { Oxs_AtlasVectorField {
      atlas :atlas
      default_value {0 0 1}
      values {
         top    {0 0 1}
      }
   }}
} ]

Specify Oxs_CGEvolve {}

Specify Oxs_MinDriver [ subst {
 basename CoFeB
 evolver Oxs_CGEvolve
 stopping_mxHxm 0.03
 mesh :mesh
 Ms  { Oxs_AtlasScalarField {
      atlas :atlas
      default_value 0
      values {
         top $Mstop
      }
 }}
 m0  { Oxs_AtlasVectorField {
     atlas :atlas
     norm 1.0
     default_value {0 0 1}
     values {
        top     {0 0 1}
     }
 }}
 normalize_aveM_output 0
 report_max_spin_angle 1
} ]
